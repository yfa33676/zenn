---
title: "IF関数を和で表す"
emoji: "🗣️"
type: "tech" # tech: 技術記事 / idea: アイデア
topics: ["Excel"]
published: false
---

## IF関数と場合分け

セル``A1``に日付型の値があり、セル``B1``にその日付の元号を表示したいとします。例えば、``2000/1/1``のときは``平成``、``2025/1/1``のときは``令和``です。まずは、``平成``、``令和``に対応しましょう。このとき、セル``B2``に

```
=IF(A1 < DATE(2019, 5, 1), "平成", "令和")
```

という式を設定すれば実現できます。

この**IF関数**による式をあえて数学の**場合分け**の記法に書き換えると、

$$
f (x) = 
\begin{cases}
   H & x < r \\
   R & x ≥ r
\end{cases}
$$

と書けます。

## 場合分けから和へ

**場合分け**は一般に

$$
f (x) = 
\begin{cases}
   f_1(x) & \cdots& P_1(x) \\
   f_2(x) & \cdots& P_2(x) \\
   &\vdots&\\
   f_n(x) & \cdots& P_n(x) \\
\end{cases}
$$

と書けます。

$P_i$ は $x \in X$ について、$P_i(x) \in \{\top, \bot\}$を与える**条件**です。つまり、$x \in X$ が $P_i$ という条件を満たすならば、$P_i(x) = \top$（真）、満たさないならば $P_i(x) = \bot$（偽）です。$\{\top, \bot\}$は**ブール代数**です。つまり、演算 $\land$（AND）と$\lor$（OR）ができる。

ブール代数は$\{0, 1\}$にも与えられます。この場合、演算 $\land$（AND）と$\lor$（OR）はそれぞれ $\cdot$（積）と$+$（和）です。ただし、この和は**冪等則**に従う必要があります。つまり、$1 + 1 = 1$ が成り立つ必要がある。

これは自然数の$\mathbb{N} = \{0, 1, 2, \cdots\}$の上では$1$以上の数をすべて$1$と考えることと同じです。つまり、$1 + 1 = 2$ ですが、$2$は$1$以上なので $1 + 1 = 2 = 1$ と考えるわけです。

さて、$f(x)$ を $P_i(x) \in \{0, 1\}$ を係数とする**線形和**として次のように表してみます。

$$
\begin{align*}
f(x) &= P_1(x)f_1(x) + P_2(x) f_2(x) + \cdots + P_n(x) f_n(x)\\
     &= \sum_{i=1}^{n} P_i(x) f_i(x)
\end{align*}
$$

$P_i(x) \in \{0, 1\}$ を係数にするというのは、

$$
P_i(x) f_i (x) = 
\begin{cases}
   f_i(x) & P_i(x) = 1 \\
   0 & P_i(x) = 0
\end{cases}
$$

という意味です。たとえば、$P_i = 1$ のときは、こういう計算になります。

$$
\begin{align*}
f(x) &= P_1(x)f_1(x) + P_2(x) f_2(x) + \cdots P_i(x) f_i(x) + \cdots + P_n(x) f_n(x)\\
&= 0 \cdot f_1(x) + 0 \cdot f_2(x) + \cdots 1 \cdot f_i(x) + \cdots + 0 \cdot f_n(x)\\
     &= 0 + 0 + \cdots + f_i(x) + \cdots + 0 \\
     &= f_i(x)\\
\end{align*}
$$

たしかに、場合分けになっています。

## 

さて、IF関数を場合分けに書き換えた

$$
f (x) = 
\begin{cases}
   H & x < r \\
   R & x ≥ r
\end{cases}
$$

こちらの式は線形和で書き換えると、

$$
f (x) = (x < r)H + (x ≥ r)R
$$

という式になります。ここから**IF関数**にするにはいくつかの工夫が必要です。

まず、$\{\top, \bot\}$ を $\{0, 1\}$ にする必要があります。しかし、これは**Excel**がすでにその``TRUE``と``FALSE``を``0``と``1``として取り扱うことを可能にしています。たとえば、

```
=TRUE * TRUE
```

という式は``1``と評価されるはずです。

続いて、線形和における$0$と和$+$です。結果は``平成``または``令和``ですから**文字列**です。文字列における$0$は空の文字列``""``であり、和$+$は連結演算子``&``です。

最後に係数演算です。文字列では``REPT``関数がよいです。これは文字列を与えられた整数回繰り返す文字列を返すというもので、``0``であれば``""``を、``1``であれば引数の文字列をそのまま返すので係数演算の条件を満たします。

以上で用意は整いました。

```
=IF(A1 < DATE(2019, 5, 1), "平成", "令和")
```

これは

```
=REPT("平成", A1 < DATE(2019, 5, 1)) & REPT("令和", A1 >= DATE(2019, 5, 1))
```

と書けます。改行などで整えますと、

```
= ""
& REPT("平成", A1 < DATE(2019, 5, 1))
& REPT("令和", A1 >= DATE(2019, 5, 1))
```

となります。

**IF関数**にもうひとつの条件を加えるときは**ネスト**を作る必要があります。たとえば、元号の式を明治、大正、昭和、平成、令和まで対応しようとすると、**IF関数**は

```
=IF(A1 < DATE(1912,7,30), 
    "明治",
    IF(A1 < DATE(1926,12,25), 
       "大正", 
       IF(A1 < DATE(1989,1,8), 
          "昭和", 
          IF(A1 < DATE(2019, 5, 1), 
             "平成",
             "令和"
          )
       )
    )
)
```

こちらを線形和で書くと、
```
= ""
& REPT("明治", (A1 <  DATE(1912, 7,30))*(A1 >= DATE(1900, 1, 1)))
& REPT("大正", (A1 <  DATE(1926,12,25))*(A1 >= DATE(1912, 7, 30)))
& REPT("昭和", (A1 <  DATE(1989, 1, 8))*(A1 >= DATE(1926,12,25)))
& REPT("平成", (A1 <  DATE(2019, 5, 1))*(A1 >= DATE(1989, 1, 8)))
& REPT("令和", (A1 >= DATE(2019, 5, 1)))
```
と書くことができます。